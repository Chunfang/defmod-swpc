${Units('si')}
undo off
reset
# Geometry
brick x 36.0 y 64.0 z 32.0 
volume 1 move x 0 y 0 z {-0.5*32.0}
create planar surface with plane xplane offset 0
webcut volume 1 with plane surface 7
create vertex 0 {-0.5*36.0} 0
create vertex 0 {+0.5*36.0} 0
create vertex 0 {-0.5*36.0} {-32.0}
create vertex 0 {+0.5*36.0} {-32.0}
create vertex 0 {-0.5*36.0} {-18.0}
create vertex 0 {+0.5*36.0} {-18.0}
split surface 7 across location vertex 22 location vertex 24
split surface 18 across location vertex 21 location vertex 23 
split surface 21 across location vertex 25 location vertex 26
delete vertex all
imprint all
merge all
delete body 2
# Mesh domain
volume all scheme map
volume all size {0.1*3}
mesh volume all

# Smooth mesh
group "refinecells" add Hex with x_min >= {-0.5*6.001} and x_max <= {+0.5*6.001} and y_min >= {-0.5*42.001} and y_max <= {+0.5*42.001} and z_min >= {-21.001}
 refine hex in group refinecells depth 1
delete group refinecells

#{beta=3.0}
#{loop(5)}
volume all smooth scheme condition number beta {beta} cpu 10
smooth volume all
#{beta=beta-0.2}
#{endloop}

# fault face
unmerge surface 23
nodeset 1 surface 28 
nodeset 2 surface 24 
sideset 1 surface 28 
sideset 2 surface 24 

# Domain boundaries
nodeset 3 surface all with x_coord > 18 - .01 
nodeset 4 surface all with y_coord > 32 - .01
nodeset 5 surface all with z_coord < -32 + .01 
sideset 3 surface all with x_coord > 18 - .01 
sideset 4 surface all with y_coord > 32 - .01
sideset 5 surface all with z_coord < -32 + .01
nodeset 6 surface all with x_coord < -18 + .01
nodeset 7 surface all with y_coord < -32 + .01
nodeset 8 surface all with z_coord > -.01
sideset 6 surface all with x_coord < -18 + .01
sideset 7 surface all with y_coord < -32 + .01
sideset 8 surface all with z_coord > -.01

group 'xneg' add hex in volume 3
group 'xpos' add hex in volume 1 
block 1 group xneg 
block 2 group xpos 
export mesh "SCEC102.exo" dimension 3 overwrite
quit
